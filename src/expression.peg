number = { hex | oct | bin | dec }
dec = @{ ("_" | ASCII_DIGIT)+ }
hex = @{ "0x" ~ ("_" | ASCII_HEX_DIGIT)+ }
oct = @{ "0o" ~ ("_" | ASCII_OCT_DIGIT)+ }
bin = @{ "0b" ~ ("_" | ASCII_BIN_DIGIT)+ }
ans = { "ans" }

operation = _{ add | subtract | multiply | divide | rem | lshift | rshift }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    rem      = { "%" }
    lshift   = { "<<" }
    rshift   = { ">>" }

expr = { term ~ (operation ~ term)* }
term = _{ ans | number | "(" ~ expr ~ ")" }

set_directive = @{ "set " ~ (word ~ " ")* ~ word }
word = { (ASCII_ALPHANUMERIC | "-" | "_")+ }

line = { SOI ~ (expr | set_directive) ~ EOI }

WHITESPACE = _{ " " }
